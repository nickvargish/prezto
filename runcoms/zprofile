#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Nick Vargish <nick@vargish.org>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

export EDITOR='vi'
export VISUAL='vi'
export PAGER='more'
if (( $+commands[emacs] )) ; then
	export EDITOR='emacs'
	export VISUAL='emacs'
fi

if (( $+commands[less] )) ; then
	export PAGER='less'
fi

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path

# Set the the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.

_homebin=$HOME/bin

# a list of possible binary locations. directories that you want at the
# front of your PATH should come first in this list.
_bindirs=( 
	$_homebin/links
    $_homebin/scripts
	$_homebin/noarch
	$HOME/sw/bin
	/usr/remote/bin
	/usr/local/{bin,sbin}
	/usr/games
	/usr/games/bin
 	/usr/{bin,sbin}
	/{bin,sbin}
	/sw/{bin,sbin}
 	)

path=($^_bindirs(N))

# add . to path only if not root.
[[ "$USERNAME" != "root" ]] && path+=.

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
export LESS='-F -g -i -M -R -S -w -X -z-4'

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

#
# Temporary Files
#

if [[ ! -d "$TMPDIR" ]]; then
  export TMPDIR="/tmp/$LOGNAME"
  mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"

# SHOST is used in title bar settings and icon names
_hostname=`hostname`
export SHOST=${_hostname%%\.*}
export DOMAIN=${_hostname#*\.}

# nethack!
export HACKOPTIONS='name:Bander,pickup_types:$,DECgraphics,color,dogname:Misha,catname:Molly,fruit:durian,!rest_on_space,showscore,showexp,time,!cmdassist,autodig,suppress_alert:3.4.3'
export NETHACKOPTIONS="$HACKOPTIONS"

# CVS environment
# Sourcefire CVS:
export CVSROOT=":ext:scm.sfeng.sourcefire.com:/usr/cvsroot"
export CVS_RSH=ssh
