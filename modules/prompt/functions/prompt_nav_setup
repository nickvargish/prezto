#
# Clean and functional single-line prompt
#
# Author:
#   Nick Vargish <nick@vargish.org>
#
# Features:
#
#   - Right prompt with directory
#   - Superuser indicator
#

function prompt_nav_precmd {
	#_prompt_nav_pwd="${PWD/#$HOME/~}"
	vcs_info
}

function prompt_nav_setup {
	setopt LOCAL_OPTIONS
	unsetopt XTRACE KSH_ARRAYS
	prompt_opts=(cr percent subst)

	autoload -Uz vcs_info
	autoload -Uz add-zsh-hook

	add-zsh-hook precmd prompt_nav_precmd

    local _failcolor='red'
    local vcs_info_color='%F{242}'

    # Set vcs_info parameters.
    zstyle ':vcs_info:*' enable bzr git hg svn
    zstyle ':vcs_info:*' check-for-changes true
    zstyle ':vcs_info:*' unstagedstr '!'
    zstyle ':vcs_info:*' stagedstr '+'
    zstyle ':vcs_info:*' actionformats "%S" "%r/%s/%b %u%c (%a)"
    zstyle ':vcs_info:*' formats "%u%c %b:%S" "%r/%s/%b %u%c" 
    zstyle ':vcs_info:*' nvcsformats "%~" ""

	PROMPT='%B%n%b%(?.@.%F{'$_failcolor'}@%f)%B%m%b%(#.#.$) '
	RPROMPT='${vcs_info_msg_0_}'
	#RPROMPT='  %38<…<%~ '
	#RPROMPT='  %38<…<%~ ${vcs_info_msg_0_%%.}'
}

prompt_nav_setup "$@"
